\begin{lstlisting}[caption={modello della classe Train}]
Class Train is

  Signals:
    start, wait, aborted, retry;

  Vars:
    routes: int[];
    route_pos: int := 0;
    nodes: obj[];
    position: int := 0;
    stops: obj[];
    stop_pos: int := 0;
    node: obj;
    aborting: bool[];

  State Top = READY, WAITING_OK, MOVING, ARRIVED, STOPPED, ABORTING, CANCELLED

  Transitions:
    READY -> WAITING_OK { - / 
    nodes[position].req(self, nodes[position], routes[route_pos]); }

    WAITING_OK -> CANCELLED { wait }  
    WAITING_OK -> MOVING { start }
    WAITING_OK -> ABORTING { - [aborting[position] = True] / 
    nodes[position].aborting(self, nodes[position], routes[route_pos]); }
    
    CANCELLED -> READY

    MOVING -> ABORTING { - [aborting[position] = True 
    and position < nodes.length - 1 and stops[stop_pos] /= nodes[position]] / 
    nodes[position].aborting(self, nodes[position], routes[route_pos]); }
    
    MOVING -> MOVING { - [aborting[position] = False 
    and position < nodes.length - 1 
    and stops[stop_pos] /= nodes[position]] /                    
                   node := null; --perche' queste azioni non sono atomiche!
                   -- sempre perche' non sono operazioni atomiche
                   -- il treno si trova per un istante su due nodi cosi da
                   -- evitare una doppia prenotazione. Quindi si setta prima 
                   -- su ON il nuovo
                   -- nodo e poi su OFF il nodo che viene lasciato.
    nodes[position + 1].sensorOn(self, nodes[position + 1], routes[route_pos]);
    nodes[position].sensorOff(self, nodes[position], routes[route_pos]);
    position := position + 1;
    node := nodes[position];
    }

    MOVING -> STOPPED { - [stops[stop_pos] = nodes[position] 
    and position < nodes.length - 1] 
    / nodes[position].train_stopped(self); }
    MOVING -> ARRIVED  { - [position = nodes.length - 1] 
    / node := null; 
    node := nodes[position].sensorOff(self, nodes[position], routes[route_pos]); }
    
    STOPPED -> READY { - [position < nodes.length - 1] /
                   stop_pos := stop_pos + 1;
                   route_pos := route_pos + 1;
    }
    
    ABORTING -> READY { aborted / 
    aborting[position] := False; }
    ABORTING -> MOVING { retry / 
    aborting[position] := False; aborting[position + 1] := True; }

end Train
\end{lstlisting}
\begin{lstlisting}[caption={modello della classe TrackCircuit}]
Class TrackCircuit is

  Signals:
    req(sender: obj, dest: obj, route: int);        
    ack(sender: obj, dest: obj, route: int);
    nack(sender: obj, dest: obj, route: int);
    commit(sender: obj, dest: obj, route: int);
    agree(sender: obj, dest: obj, route: int);
    disagree(sender: obj, dest: obj, route: int);
    train_stopped(sender: obj);
    aborting(sender: obj, dest: obj, route: int);
    cancel(sender: obj, dest: obj, route: int);

  Operations:
    sensorOn(sender: obj, dest: obj, route: int);
    sensorOff(sender: obj, dest: obj, route: int);

  Vars:
    next: obj[];
    prev: obj[];
    train: obj := null;
    outer_station: bool;

  State Top = FREE, WAITING_ACK, WAITING_COMMIT, WAITING_AGREE, RESERVED, 
  TRAIN_PASSING, ABORTING

  Transitions:
    FREE -> WAITING_ACK { req(sender, dest, route) [(train=null or sender=train) 
    and next[route] /= null] /
     next[route].req(self, next[route], route); }
    FREE -> WAITING_COMMIT { req(sender, dest, route) [(train=null or sender=train) 
    and next[route] = null] / 
    prev[route].ack(self, prev[route], route); }
    FREE -> FREE { req(sender, dest, route) [train/=null and sender/=train] 
    / sender.nack(self, sender, route); }

    WAITING_ACK -> WAITING_COMMIT { ack(sender, dest, route) 
    [prev[route] /= null and train = null] / 
    prev[route].ack(self, prev[route], route); }
    WAITING_ACK -> WAITING_AGREE{ ack(sender, dest, route) [train /= null] / 
    next[route].commit(self, next[route], route); }
    WAITING_ACK -> FREE { nack(sender, dest, route_id) 
    [(prev[route] = null) 
    and (train /= null)] / 
    train.wait; }
    WAITING_ACK -> FREE { nack(sender, dest, route_id) 
    [prev[route] /= null] / 
    prev[route].nack(self, prev[route], route); }
    WAITING_ACK -> WAITING_ACK { req(sender, dest, route) / 
    sender.nack(self, sender, route); }
    WAITING_ACK -> ABORTING { aborting(sender, dest, route) / 
    next[route].aborting(self, next[route], route); }

    WAITING_COMMIT -> WAITING_AGREE{ commit(sender, dest, route) 
    [next[route] /= null] /  next[route].commit(self, next[route], route); }
    WAITING_COMMIT -> Top.RESERVED{ commit(sender, dest, route) 
    [next[route] = null] / 
    prev[route].agree(self, prev[route], route); }
    WAITING_COMMIT -> WAITING_COMMIT { req(sender, dest, route) / 
    sender.nack(self, sender, route); }
    WAITING_COMMIT -> ABORTING { aborting(sender, dest, route) 
    [next[route] /= null] / 
    next[route].aborting(self, next[route], route); }
    WAITING_COMMIT -> FREE { aborting(sender, dest, route) 
    [next[route] = null] / 
    prev[route].cancel(self, prev[route], route); }

    WAITING_AGREE -> Top.RESERVED { agree(sender, dest, route) [prev[route] /= null 
    and train = null] / prev[route].agree(self, prev[route], route); }
    WAITING_AGREE -> Top.RESERVED { agree(sender, dest, route) [train /= null] / 
    train.start; }
    WAITING_AGREE -> FREE { disagree(sender, dest, route_id) 
    [(prev[route] = null) and (train /= null)] / 
    train.wait; }
    WAITING_AGREE -> FREE { disagree(sender, dest, route_id) 
    [prev[route] /= null] / 
    prev[route].disagree(self, prev[route], route); }
    WAITING_AGREE -> WAITING_AGREE { req(sender, dest, route) / 
    sender.nack(self, sender, route); }
    WAITING_AGREE -> ABORTING { aborting(sender, dest, route) / 
    next[route].aborting(self, next[route], route); }

    Top.RESERVED -> TRAIN_PASSING { sensorOn(sender,dest,route) / 
    train := sender; }
    Top.RESERVED -> FREE { disagree(sender, dest, route) / 
    if next[route] /= null then 
    { next[route].disagree(self, next[route], route) }; }
    Top.RESERVED -> FREE { sensorOff(sender,dest,route) / 
    train := null; }
    Top.RESERVED -> Top.RESERVED { req(sender, dest, route) / 
    sender.nack(self, sender, route); }
    Top.RESERVED -> ABORTING { aborting(sender, dest, route) 
    [next[route] /= null] / 
    next[route].aborting(self, next[route], route); }
    Top.RESERVED -> FREE { aborting(sender, dest, route) 
    [next[route] = null] / 
    prev[route].cancel(self, prev[route], route); }

    TRAIN_PASSING -> FREE { sensorOff(sender,dest,route) 
    [next[route] = null] / 
    if(outer_station = True) { train := null; return null;} 
    else { return self; } }
    TRAIN_PASSING -> FREE { sensorOff(sender,dest,route) 
    [next[route] /= null] / 
    train := null; }
    TRAIN_PASSING -> FREE { train_stopped(sender) 
    [train = sender] }
    TRAIN_PASSING -> TRAIN_PASSING { req(sender, dest, route) / 
    sender.nack(self, sender, route); }
    TRAIN_PASSING -> ABORTING { aborting(sender, dest, route) / 
    next[route].aborting(self, next[route], route); }

    ABORTING -> FREE { cancel(sender, dest, route) 
    [train /= null] / 
    train.aborted; }
    ABORTING -> FREE { cancel(sender, dest, route) 
    [train = null and prev[route] /= null] / 
    prev[route].cancel(self, prev[route], route); }

end TrackCircuit

\end{lstlisting}
\begin{lstlisting}[caption={modello della classe Switch}]
Class Switch is

  Signals:
    req(sender: obj, dest: obj, route: int);        
    ack(sender: obj, dest: obj, route: int);
    nack(sender: obj, dest: obj, route: int);
    commit(sender: obj, dest: obj, route: int);
    agree(sender: obj, dest: obj, route: int);
    disagree(sender: obj, dest: obj, route: int);
    aborting(sender: obj, dest: obj, route: int);
    cancel(sender: obj, dest: obj, route: int);

  Operations:
    sensorOn(sender: obj, dest: obj, route: int);  
    sensorOff(sender: obj, dest: obj, route: int);

  Vars:
    next: obj[];             
    prev: obj[];             
    conf: bool[];
    reversed: bool := False; 
    train: obj := null;      
    requested_route: int;         

  State Top = FREE, WAITING_ACK, WAITING_COMMIT, 
  WAITING_AGREE, POSITIONING, RESERVED, 
  TRAIN_PASSING, CHECK_POSITION, ABORTING
  
  State WAITING_ACK Defers req(sender: obj, dest: obj, route: int)

  Transitions:
    FREE -> WAITING_ACK { req(sender, dest, route) / 
    next[route].req(self, next[route], route); }

    WAITING_ACK -> WAITING_COMMIT { ack(sender, dest, route) / 
    prev[route].ack(self, prev[route], route); }
    WAITING_ACK -> FREE { nack(sender, dest, route) / 
    prev[route].nack(self, prev[route], route); }
    WAITING_ACK -> WAITING_ACK { req(sender, dest, route) / 
    sender.nack(self, sender, route); }
    WAITING_ACK -> ABORTING { aborting(sender, dest, route) / 
    next[route].aborting(self, next[route], route); }

    WAITING_COMMIT -> WAITING_AGREE { commit(sender, dest, route) / 
    				next[route].commit(self, next[route], route); }
    WAITING_COMMIT -> WAITING_COMMIT { req(sender, dest, route) / 
    sender.nack(self, sender, route); }
    WAITING_COMMIT -> ABORTING { aborting(sender, dest, route) / 
    next[route].aborting(self, next[route], route); }

    WAITING_AGREE -> FREE { disagree(sender, dest, route) / 
    prev[route].disagree(self, prev[route], route); }
    WAITING_AGREE -> WAITING_AGREE { req(sender, dest, route) / 
    sender.nack(self, sender, route); }
    WAITING_AGREE -> CHECK_POSITION { agree(sender, dest, route) / 
    requested_route := route; }
    WAITING_AGREE -> ABORTING { aborting(sender, dest, route) / 
    next[route].aborting(self, next[route], route); }

    CHECK_POSITION -> Top.RESERVED { - [reversed = conf[requested_route]] / 
    prev[requested_route].agree(self, prev[requested_route], 
    requested_route); }
    CHECK_POSITION -> POSITIONING { - [reversed /= conf[requested_route]] }
    CHECK_POSITION -> CHECK_POSITION { req(sender, dest, route) / 
    sender.nack(self, sender, route); }
    CHECK_POSITION -> ABORTING { aborting(sender, dest, route) / 
    next[route].aborting(self, next[route], route); }

    POSITIONING -> Top.RESERVED { - / reversed := not reversed; 
    prev[requested_route].agree(self, prev[requested_route], 
    requested_route); }
    POSITIONING -> FREE  { - / 
    prev[requested_route].disagree(self, prev[requested_route], 
    requested_route); 
    next[requested_route].disagree(self, next[requested_route], 
    requested_route); }
    POSITIONING -> POSITIONING { req(sender, dest, route) / 
    sender.nack(self, sender, route); }
    POSITIONING -> ABORTING { aborting(sender, dest, route) / 
    next[route].aborting(self, next[route], route); }

    Top.RESERVED -> TRAIN_PASSING { sensorOn(sender,dest,route) / 
    train := sender; }
    Top.RESERVED -> FREE { disagree(sender, dest, route) / 
    next[route].disagree(self, next[route], route); }
    Top.RESERVED -> Top.RESERVED { req(sender, dest, route) / 
    sender.nack(self, sender, route); }
    Top.RESERVED -> ABORTING { aborting(sender, dest, route) / 
    next[route].aborting(self, next[route], route); }

    TRAIN_PASSING -> FREE { sensorOff(sender,dest,route) / 
    train := null; }
    TRAIN_PASSING -> TRAIN_PASSING { req(sender, dest, route) / 
    sender.nack(self, sender, route); }
    TRAIN_PASSING -> TRAIN_PASSING { aborting(sender, dest, route) 
    [train /= null and sender = train] / train.retry; }

    ABORTING -> FREE { cancel(sender, dest, route) / 
    prev[route].cancel(self, prev[route], route); }

end Switch
\end{lsrlisting}

\begilsstlisting}[caption={modello della classe Semaphore}]
Class Semaphore is

  Signals:
    req(sender: obj, dest: obj, route: int);        
    ack(sender: obj, dest: obj, route: int);
    nack(sender: obj, dest: obj, route: int);
    commit(sender: obj, dest: obj, route: int);
    agree(sender: obj, dest: obj, route: int);
    disagree(sender: obj, dest: obj, route: int);
    aborting(sender: obj, dest: obj, route: int);
    cancel(sender: obj, dest: obj, route: int);

  Operations:
    sensorOn(sender: obj, dest: obj, route: int);  
    sensorOff(sender: obj, dest: obj, route: int);

  Vars:
    next: obj[];             
    prev: obj[];             
    train: obj := null;      
    requested_route: int;
    color_red: bool := True ;          

  State Top = RED, WAITING_ACK, WAITING_COMMIT, WAITING_AGREE, 
  COMMANDED_RED, COMMANDED_GREEN, TRAIN_PASSING, GREEN, ABORTING, FAILED
  
  State WAITING_ACK Defers req(sender: obj, dest: obj, route: int)

  Transitions:
    RED -> WAITING_ACK { req(sender, dest, route) / 
    next[route].req(self, next[route], route); }

    WAITING_ACK -> WAITING_COMMIT { ack(sender, dest, route) / 
    prev[route].ack(self, prev[route], route); }
    WAITING_ACK -> RED { nack(sender, dest, route) / 
    prev[route].nack(self, prev[route], route); }
    WAITING_ACK -> WAITING_ACK { req(sender, dest, route) / 
    sender.nack(self, sender, route); }
    WAITING_ACK -> ABORTING { aborting(sender, dest, route) / 
    next[route].aborting(self, next[route], route); }

    WAITING_COMMIT -> WAITING_AGREE { commit(sender, dest, route) / 
    next[route].commit(self, next[route], route); }
    WAITING_COMMIT -> WAITING_COMMIT { req(sender, dest, route) / 
    sender.nack(self, sender, route); }
    WAITING_COMMIT -> ABORTING { aborting(sender, dest, route) / 
    next[route].aborting(self, next[route], route); }

    WAITING_AGREE -> RED { disagree(sender, dest, route) / 
    prev[route].disagree(self, prev[route], route); }
    WAITING_AGREE -> WAITING_AGREE { req(sender, dest, route) / 
    sender.nack(self, sender, route); }
    WAITING_AGREE -> COMMANDED_GREEN { agree(sender, dest, route) / 
    requested_route := route; }
    WAITING_AGREE -> ABORTING { aborting(sender, dest, route) / 
    next[route].aborting(self, next[route], route); }

    COMMANDED_GREEN -> Top.GREEN { - / 
    prev[requested_route].agree(self, 
    prev[requested_route], requested_route); 
    color_red := False; }
    COMMANDED_GREEN -> RED  { - / 
    prev[requested_route].disagree(self, 
    prev[requested_route], requested_route); 
    next[requested_route].disagree(self, 
    next[requested_route], requested_route); }
    
    COMMANDED_GREEN -> COMMANDED_GREEN { req(sender, dest, route) / 
    sender.nack(self, sender, route); }
    
    COMMANDED_GREEN -> ABORTING { aborting(sender, dest, route) / 
    next[route].aborting(self, next[route], route); }

    Top.GREEN -> TRAIN_PASSING { sensorOn(sender,dest,route) / 
    train := sender; }
    Top.GREEN -> Top.GREEN { req(sender, dest, route) / 
    sender.nack(self, sender, route); }
    Top.GREEN -> ABORTING { aborting(sender, dest, route) / 
    next[route].aborting(self, next[route], route); }

    TRAIN_PASSING -> COMMANDED_RED { sensorOff(sender,dest,route) / 
    train := null; }
    TRAIN_PASSING -> TRAIN_PASSING { req(sender, dest, route) / 
    sender.nack(self, sender, route); }
    TRAIN_PASSING -> TRAIN_PASSING { aborting(sender, dest, route) 
    [train /= null and sender = train] / train.retry; }
 
    COMMANDED_RED -> COMMANDED_RED { req(sender, dest, route) / 
    sender.nack(self, sender, route); }
    COMMANDED_RED -> FAILED
    COMMANDED_RED -> RED {- / 
    color_red := True;}
    
    FAILED -> FAILED { req(sender, dest, route) / 
    sender.nack(self, sender, route); }
    FAILED -> RED {- / 
    color_red := True;}

    ABORTING -> RED { cancel(sender, dest, route) / 
    prev[route].cancel(self, prev[route], route); }

end Semaphore
\end{lstlisting}

\begin{lstlisting}[caption={oggetti istanziati nella simulazione}]
Objects

GA1: TrackCircuit (
 train => null, 
    outer_station => True,
 prev => [null,null,null,null,null,null,W1,null,W1,null,W1,null,W1
 ,null,null],
 next => [A,A,A,A,A,null,null,null,null,null,null,null,null,null,
 null]);
A: Semaphore (
    train => null,
 prev => [GA1,GA1,GA1,GA1,GA1,null,null,null,null,null,null,null,
 null,null,null],
 next => [W1,W1,W1,W1,W1,null,null,null,null,null,null,null,null,
 null,null]);
W1: Switch (
 conf => [False, False, False, False, False, False, False, False, 
 False, False, False, False, False, True, True],
    train => null,
 prev => [A,A,A,A,A,null,P1,null,P2,null,P1,null,
 P2,null,null],
 next => [GA2,GA2,GA3,GA3,GA3,null,GA1,null,GA1,null,
 GA1,null,GA1,null,null]);
GA2: TrackCircuit (
 train => null, 
    outer_station => False,
 prev => [W1,W1,null,null,null,W2,W2,null,null,null,null,null,
 null,null,null],
 next => [null,N1,null,null,null,null,P1,null,null,null,P1,N1,
 null,null,null]);
N1: Semaphore (
    train => null,
 prev => [null,GA2,null,null,null,null,null,null,null,null,null,
 GA2,null,null,null],
 next => [null,W2,null,null,null,null,null,null,null,null,null,
 W2,null,null,null]);
W2: Switch (
 conf => [False, False, False, False, False, False, False, False, 
 False, False, False, 
 False, False, True, True],
 prev => [null,N1,null,W3,null,F,F,F,F,null,null,N1,null,W3,null],
 next => [null,GA4,null,GA4,null,GA2,GA2,W3,W3,null,null,GA4,null,
 GA4,null]);
GA4: TrackCircuit (
 train => null, 
    outer_station => True,
 prev => [null,W2,null,W2,null,null,null,null,null,null,null,W2,
 null,W2,null],
 next => [null,null,null,null,null,F,F,F,F,null,null,null,null,
 null,null]);
GA3: TrackCircuit (
 train => locomotive, 
    outer_station => False,
 prev => [null,null,W1,W1,W1,null,null,W3,W3,W3,null,null,null,
 null,null],
 next => [null,null,null,N2,N2,null,null,null,P2,null,null,null,
 P2,N2,N2]);
N2: Semaphore (
    train => null,
 prev => [null,null,null,GA3,GA3,null,null,null,null,null,null,
 null,null,GA3,GA3],
 next => [null,null,null,W3,W3,null,null,null,null,null,null,
 null,null,W3,W3]);
W3: Switch (
 conf => [False, False, False, False, False, False, False, 
 False, False, False, False, 
 False, False, True, True],
 prev => [null,null,null,N2,N2,null,null,W2,W2,S10,null,
 snull,null,N2,N2],
 next => [null,null,null,W2,GA5,null,null,GA3,GA3,GA3,null,
 null,null,W2,GA5]);
GA5: TrackCircuit (
 train => null, 
    outer_station => False,
 prev => [null,null,null,null,W3,null,null,null,null,null,null,null,
 null,null,W3],
 next => [null,null,null,null,null,null,null,null,null,S10,null,null,
 null,null,null]);
F: Semaphore (
    train => null,
 prev => [null,null,null,null,null,GA4,GA4,GA4,GA4,null,null,null,null,
 null,null],
 next => [null,null,null,null,null,W2,W2,W2,W2,null,null,null,null,null,null]);
P1: Semaphore (
    train => null,
 prev => [null,null,null,null,null,null,GA2,null,null,null,GA2,null,null,
 null,null],
 next => [null,null,null,null,null,null,W1,null,null,null,W1,null,null,null,null]);
P2: Semaphore (
    train => null,
 prev => [null,null,null,null,null,null,null,null,GA3,null,null,null,GA3,
 null,null],
 next => [null,null,null,null,null,null,null,null,W1,null,null,null,W1,
 null,null]);
S10: Semaphore (
    train => null,
 prev => [null,null,null,null,null,null,null,null,null,GA5,null,null,null,
 null,null],
 next => [null,null,null,null,null,null,null,null,null,W3,null,null,null,
 null,null]);
 
 locomotive: Train (
 	routes => [ 1,6,3,8,0,10,2,12,4,9,13,5,11,7,14],
 	nodes => [ GA1,A,W1,GA2,N1,W2,GA4,F,W2,GA2,P1,W1,GA1,A,W1,GA3,N2,W3,W2,
 	GA4,F,W2,W3,
 	GA3,P2,W1,GA1,A,W1,GA2,P1,W1,GA1,A,W1,GA3,P2,W1,GA1,A,W1,GA3,N2,W3,GA5,
 	S10,W3,GA3,
 	N2,W3,W2,GA4,F,W2,GA2,
 	N1,W2,GA4,F,W2,W3,GA3,N2,W3,GA5],
 	aborting => [False,False,False,False,False,False,False,False,False,False,
 	False,False,False,False,False,False,False,False,False,False,False,False,
 	False,False,False,False,False,False,False,False,False,False,False,False,
 	False,False,False,False,False,False,False,False,False,False,False,False,
 	False,False,False,False,False,False,False,False,
 	False,False,False,False,False,False,False,False,
 	False,False,False],
 	stops => [GA4,GA1,GA4,GA1,GA2,GA1,GA3,GA1,GA5,GA3,GA4,GA2,
 	GA4,GA3,GA5],
 	node => GA1
 );
 
\end{lstlisting}

\begin{lstlisting}[caption={Astrazioni per la verifica delle proprieta'}]
Abstractions {
  Action $1($*) -> $1($*)
    
  State inState(locomotive.ARRIVED) -> locomotive_arrived

  State inState(locomotive.CANCELLED) -> locomotive_cancelled

  State inState(locomotive.WAITING_OK) -> locomotive_request
  }
\end{lstlisting}